jar {
    dependsOn addHashFile
    baseName project.rootProject.name
    from 'git.properties'
}

bootRepackage {
    withJarTask = 'jar'
}

//Make sure that fat jar is built before uploading
project.tasks.findAll { it.name.startsWith('publish') || it.name == 'bintrayUpload' }*.dependsOn { bootRepackage }

publishing {
    repositories {
        maven {
            url getProp('mavenRepoUrl')
            credentials {
                username getProp('mavenUser')
                password getProp('mavenPassword')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId "${project.rootProject.name}"
            from components.java
            pom.withXml {
                //Remove dependencies - everything is embedded in JAR created by Spring Boot
                asNode().dependencies.replaceNode { null }
            }
        }
    }
}

task uploadArchives(dependsOn: 'publish', group: 'publishing') {
    description "DEPRECATED - Publishes produced archives - DEPRECATED - use 'publish' task"
    doFirst {
        logger.warn("DEPRECATION WARNING. Task 'uploadArchives' is deprecated. Use 'publish' task instead.")
    }
}

String getProp(String propName) {
    return hasProperty(propName) ? (getProperty(propName) ?: System.properties[propName]) : System.properties[propName]
}